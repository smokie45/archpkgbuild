# Maintainer: Christopher Arndt <aur -at- chrisarndt -dot- de>

_projname='pigpio'
pkgname="python-${_projname}"
pkgver=74
pkgrel=1
pkgdesc="A C and Python library for controlling GPIOs on a Raspberry Pi"
url="http://abyz.me.uk/rpi/pigpio/"
license=('custom:UNLICENSE')
arch=('aarch64' 'armv6h' 'armv7h' 'i686' 'x86_64')
depends=('python')
source=("${_projname}-${pkgver}.tar.gz::https://github.com/joan2937/pigpio/archive/v${pkgver}.tar.gz")
sha256sums=('dd377d0109e20437b323d94523eda1dced6c63f6f347fa564182bc18a3e126aa')


prepare() {
  cd "${_projname}-${pkgver}"
  sed -e '/which python2/d' -i Makefile
  sed -e '/\/opt/d' -i Makefile
  sed -e 's|\$(prefix)/man|\$(prefix)/share/man|' -i Makefile
  sed -e 's|/usr/bin/pigpiod|/usr/bin/pigpiod -g|' -i util/pigpiod.service
}

build() {
  cd "${_projname}-${pkgver}"
  make
}

package() {
  cd "${_projname}-${pkgver}"
  make prefix=/usr DESTDIR="${pkgdir}" install
  install -Dm644 util/pigpiod.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm644 UNLICENCE -t "${pkgdir}/usr/share/licenses/$pkgname"
}
