# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Bob Finch <w9ya@qrparci.net>

pkgname="portaudio-git"
# versioning is very bizarre:
# https://app.assembla.com/wiki/show/portaudio/ReleaseNotes
_version=190600_20161030
pkgver=r603.09087cf
pkgrel=1
epoch=1
pkgdesc='A free, cross-platform, open source, audio I/O library.'
arch=('x86_64' 'armv7h')
url="http://www.portaudio.com/"
license=('MIT')
depends=( 'alsa-lib')
provides=('libportaudio.so' 'libportaudiocpp.so')
source=(git+https://git.assembla.com/portaudio.git)
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${pkgname%-git}"
  ./configure --prefix=/usr \
              --enable-cxx  \
              --without-oss
  make -j1
}

package() {
  cd "${srcdir}/${pkgname%-git}"
  make DESTDIR="${pkgdir}/" install
}
